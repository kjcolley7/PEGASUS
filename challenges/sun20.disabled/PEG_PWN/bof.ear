/*
int gets(
	char* buffer               // A0
) {
	int bytes_read = 0;        // RV
	int value;                 // R3
	int prev_value = 0;        // R4
	
	while((value = port_read(stdio)) >= 0) {
		if(value == '\n') {
			*buffer = prev_value;
			break;
		}
		
		if(bytes_read != 0) {
			*buffer++ = prev_value | 0x80;
		}
		
		prev_value = value;
		++bytes_read;
	}
	
	if(value < 0) {
		return value;
	}
	
	return bytes_read;
}
*/


// A0 = buffer
// RV = bytes_read
// R3 = value
// R4 = prev_value
.scope
.export @gets
@gets:
	// bytes_read = 0;
	MOV     RV, ZERO
	MOV     R4, ZERO
	BRR     @.while_cond
	
@.while_body:
	/*
	if(value == '\n') {
		*buffer = prev_value;
		break;
	}
	*/
	CMP     R3, '\n'
	STB.EQ  [A0], R4
	BRR.EQ  @.return
	
	/*
	if(bytes_read != 0) {
		*buffer++ = prev_value | 0x80;
	}
	*/
	MOV     RV, RV
	BRR.ZR  @.skip_first_write
	
	ORR     R4, 0x80
	STB     [A0], R4
	INC     A0
	
@.skip_first_write:
	// prev_value = value;
	MOV     R4, R3
	
	// ++bytes_read;
	INC     RV
	
@.while_cond:
	// while(value = port_read(portnum)) >= 0)
	RDB     R3
	BRR.CC  @.while_body
	// if(value < 0) return value;
	MOV     RV, R3
	
	// return bytes_read;
@.return:
	RET


/*
void puts(
	const char* s //A0
) {
	if(s == NULL) {
		return;
	}
	
	char byte;         //R2
	u16 c;             //R3
	u16 ascii = 0x7f;  //R4
	do {
		byte = *s++;
		c = byte & ascii;
		WRB(c);
	} while(byte != c);
}
*/
.scope
.export @puts
@puts: //LEAF
	MOV     A0, A0                 // NULL check (more lightweight than CMP)
	RET.EQ                         // Return if NULL
	
	MOV     R4, 0x7f               // Create mask for the ASCII character bits
	
@.next_char:
	LDB     R2, [A0]               // Load character from string
	INC     A0                     // Increment string pointer
	AND     R3, R2, R4             // Mask off the continuation bit
	WRB     R3                     // Write ASCII character
	CMP     R2, R3                 // Check for continuation bit
	BRR.NE  @.next_char            // Continuation bit was set, keep iterating through the string
	
	RET

.scope
.export @save_regs
@save_regs:
	PSH     {RA, RD}
	PSH     A0, {A1-A5}
	POP     {PC, DPC}

.scope
.export @login
@login:
	PSH     {A1-A5, S0, FP, RA, RD}
	MOV     FP, SP
	SUB     SP, 50
	
	MOV     S0, A0
	
	ADR     A0, @prompt_password
	FCR     @puts
	
	MOV     A0, S0
	FCR     @puts
	
	WRB     ':'
	WRB     ' '
	
	MOV     A0, SP
	FCR     @gets
	
	ADR     A0, @login_success
	FCR     @puts
	
	MOV     A0, ZERO
	MOV     SP, FP
	POP     {A1-A5, S0, FP, PC, DPC}

.scope
.export @main
@main:
	SUB     SP, 50
	
	ADR     A0, @prompt_user
	FCR     @puts
	
	MOV     A0, SP
	FCR     @gets
	
	MOV     A0, SP
	FCR     @login
	
	ADR     A0, @login_success
	FCR     @puts
	HLT

@prompt_user:
	.lestring "Enter username: "

@prompt_password:
	.lestring "Enter password for "

@login_success:
	.lestring "Login success!\n"
